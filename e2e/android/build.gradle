// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext {
        buildToolsVersion = findProperty('android.buildToolsVersion') ?: '33.0.0'
        minSdkVersion = Integer.parseInt(findProperty('android.minSdkVersion') ?: '21')
        compileSdkVersion = Integer.parseInt(findProperty('android.compileSdkVersion') ?: '33')
        targetSdkVersion = Integer.parseInt(findProperty('android.targetSdkVersion') ?: '33')
        kotlinVersion = findProperty('android.kotlinVersion') ?: '1.8.10'
        frescoVersion = findProperty('expo.frescoVersion') ?: '2.5.0'

        // We use NDK 23 which has both M1 support and is the side-by-side NDK version from AGP.
        ndkVersion = "23.1.7779620"
    }
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath libs.gradle
        classpath libs.kotlin.gradle.plugin
        classpath('com.facebook.react:react-native-gradle-plugin')
        classpath('com.diffplug.spotless:spotless-plugin-gradle:6.12.0')
    }
}

plugins {
    id 'org.jetbrains.kotlin.android' version "1.8.10" apply false
}

allprojects {
    repositories {
        maven {
            // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
            url(new File(['node', '--print', "require.resolve('react-native/package.json')"].execute(null, rootDir).text.trim(), '../android'))
        }
        maven {
            // Android JSC is installed from npm
            url(new File(['node', '--print', "require.resolve('jsc-android/package.json')"].execute(null, rootDir).text.trim(), '../dist'))
        }

        google()
        mavenCentral()
        maven { url 'https://www.jitpack.io' }
    }
    configureSpotless(it)
}

def configureSpotless(Project project) {
    project.apply plugin: 'com.diffplug.spotless'

    project.spotless {
        format 'misc', {
            target '**/*.gradle', '**/*.md', '**/.gitignore', '**/*.xml', '**/*.json', '**/*.properties', '**/*.yml', '**/*.kt'
            targetExclude '.idea/**', '**/build/**', '.gradle/**'

            trimTrailingWhitespace()
            endWithNewline()

        }
    }

    project.plugins.withId('kotlin-android') {
        project.spotless {
            kotlin {
                target '**/*.kt'
                targetExclude '**/build/**'
                ktlint()
                        .editorConfigOverride([
                                ktlint_disabled_rules : "package-name"
                        ])
                licenseHeaderFile rootProject.file('licenseTemplate.txt')
            }
        }
    }
}
